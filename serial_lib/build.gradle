plugins {
    id 'com.android.library'
    id 'maven-publish'
}

android {
    namespace "com.cl.serialportlibrary"
    compileSdk 34

    defaultConfig {
        minSdk 21
        targetSdk 34
        versionCode 508
        versionName "5.0.8"
        consumerProguardFiles "consumer-rules.pro"
        
        externalNativeBuild {
            cmake {
                // 设置 Android 平台和 STL
                arguments "-DANDROID_PLATFORM=android-21",
                          "-DANDROID_STL=c++_shared"
            }
        }
        ndk {
            abiFilters 'arm64-v8a', 'armeabi-v7a', 'x86', 'x86_64'
        }
    }
    
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
        }
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation 'androidx.appcompat:appcompat:1.6.1'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId = 'com.github.cl-6666'
                artifactId = 'serialPort'
                version = '5.0.6'
                
                artifact bundleReleaseAar
                
                pom {
                    name = 'SerialPort Library'
                    description = 'Android Serial Port Library with JNI support'
                    url = 'https://github.com/cl-6666/serialPort'
                    
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    
                    developers {
                        developer {
                            id = 'cl-6666'
                            name = 'cl-6666'
                        }
                    }
                    
                    scm {
                        connection = 'scm:git:git://github.com/cl-6666/serialPort.git'
                        developerConnection = 'scm:git:ssh://github.com:cl-6666/serialPort.git'
                        url = 'https://github.com/cl-6666/serialPort/tree/master'
                    }
                }
            }
        }
    }
}

